varnishtest "3686: Server disconnects after reading the headers but sends a response first"

barrier b1 cond 2

server s1 {
        rxreqhdrs
        txresp -status 400 -body "Invalid request"
} -start

varnish v1 -vcl+backend {
	import vtc;

	sub vcl_backend_response {
        set beresp.http.br_uncacheable = beresp.uncacheable;
        set beresp.http.br_send_failed = bereq.send_failed;
        set beresp.http.br_fetch_failed = beresp.fetch_failed;
	}

	sub vcl_backend_error {
        set beresp.http.be_uncacheable = true;
        set beresp.http.be_send_failed = bereq.send_failed;
        set beresp.http.be_fetch_failed = beresp.fetch_failed;
	}

} -start

client c1 {
        txreq -method POST -hdr "Transfer-Encoding: chunked"
        chunkedlen 100
        barrier b1 sync
        chunkedlen 100
        chunkedlen 0
        rxresp
        expect resp.status == 400
        expect resp.body == "Invalid request"
        expect resp.http.br_uncacheable == true
        expect resp.http.br_send_failed == true
        expect resp.http.br_fetch_failed == false
} -start

server s1 -wait
barrier b1 sync
client c1 -wait
